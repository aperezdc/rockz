#compdef rockz

local -a rockz_commands
for file in "${_rockz_dir}"/doc/cmd-*.txt ; do
	local cmd=${file#*/cmd-}
	rockz_commands+=( "${cmd%.txt}:$(read -re < "${file}")" )
done
local -a rockz_rockenvs=( $(rockz ls) )

typeset -A opt_args
local state

_arguments \
	"1: :{_describe 'command' rockz_commands -- 'rockenv' rockz_rockenvs}" \
	'*:: :->args'

case ${words[1]} in
	activate | rm)
		_arguments "1: :{_describe 'rockenv' rockz_rockenvs}"
		;;
	profile)
		local -a rockz_profiles=( $(rockz profile) )
		_arguments -s \
			":profile name:(${rockz_profiles[*]})" \
			'(-s --show)'{-s,--show=-}'[Show profile settings]' \
			'(-l --lua)'{-l,--lua=-}'[Path to Lua executable]:files:_files' \
			'(-I --include)'{-I,--include=-}'[Path to Lua headers directory]:files:_files' \
			'(-L --library)'{-L,--library=-}'[Path to the Lua library]:files:_files'
		;;
	new)
		local -a rockz_profiles=( $(rockz profile) )
		_arguments -s \
			'1:rockenv name:' \
			"(-p --profile)"{-p,--profile=-}"[Lua rockenv profile]:profile:(${rockz_profiles[*]})"
		;;
esac
